load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push", "oci_tarball")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@container_structure_test//:defs.bzl", "container_structure_test")
load("//dev:oci_defs.bzl", "image_repository")

go_library(
    name = "symbols_lib",
    srcs = ["main.go"],
    importpath = "github.com/sourcegraph/sourcegraph/cmd/symbols",
    visibility = ["//visibility:private"],
    deps = [
        "//cmd/symbols/shared",
        "//internal/sanitycheck",
        "//internal/service/svcmain",
    ],
)

go_binary(
    name = "symbols",
    embed = [":symbols_lib"],
    visibility = ["//visibility:public"],
    x_defs = {
        "github.com/sourcegraph/sourcegraph/internal/version.version": "{STABLE_VERSION}",
        "github.com/sourcegraph/sourcegraph/internal/version.timestamp": "{VERSION_TIMESTAMP}",
    },
)

pkg_tar(
    name = "tar_symbols",
    srcs = [":symbols"],
)

pkg_tar(
    name = "tar_scip-ctags",
    srcs = ["//docker-images/syntax-highlighter:scip-ctags"],
    package_dir = "/usr/local/bin",
)

oci_image(
    name = "image",
    base = "@wolfi_symbols_base",
    entrypoint = [
        "/sbin/tini",
        "--",
        "/symbols",
    ],
    env = {
        "CACHE_DIR": "/mnt/cache/symbols",
    },
    tars = [
        ":tar_symbols",
        ":tar_scip-ctags",
    ],
)

oci_tarball(
    name = "image_tarball",
    image = ":image",
    repo_tags = ["symbols:candidate"],
)

container_structure_test(
    name = "image_test",
    timeout = "short",
    configs = ["image_test.yaml"],
    driver = "docker",
    image = ":image",
    tags = [
        "exclusive",
        "requires-network",
    ],
)

oci_push(
    name = "candidate_push",
    image = ":image",
    repository = image_repository("symbols"),
)
