load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//client/web-sveltekit:vite/package_json.bzl", vite_bin = "bin")
load("@npm//client/web-sveltekit:vitest/package_json.bzl", vitest_bin = "bin")
load("//dev:defs.bzl", "vitest_test")

# gazelle:ignore

SRCS = [
    "package.json",
    "vite.config.ts",
    "svelte.config.js",
    "tsconfig.json",
    ".eslintignore",
    ".eslintrc.cjs",
    ".prettierignore",
    ".env",
    ".env.dotcom",
    "//client/wildcard:sass-breakpoints",
    "//client/wildcard:global-style-sources",
    "//client/web/dist/img:copy",
] + glob(
    ["src/" + d for d in [
        "**/*.scss",
        "**/*.svg",
        "**/*.ts",
        "**/*.svelte",
        "**/*.html",
        "**/*.tsx",
        "**/*.gql",
    ]],
    [
        "src/lib/graphql-operations.ts",
        "src/lib/graphql-types.ts",
        "src/lib/graphql-type-mocks.ts",
        "src/**/*.gql.ts",
        "src/**/*.gql.d.ts",
        "src/**/*.test.ts",
        "src/**/*.spec.ts",
    ],
) + glob([
    "dev/**/*.cjs",
    "dev/**/*.ts",
])

BUILD_DEPS = [
    ":node_modules/@faker-js/faker",
    ":node_modules/@floating-ui/dom",
    ":node_modules/@graphql-codegen/cli",
    ":node_modules/@graphql-codegen/near-operation-file-preset",
    ":node_modules/@graphql-codegen/typed-document-node",
    ":node_modules/@graphql-codegen/typescript",
    ":node_modules/@graphql-codegen/typescript-operations",
    ":node_modules/@graphql-tools/utils",
    ":node_modules/@melt-ui/svelte",
    ":node_modules/@sourcegraph/branded",
    ":node_modules/@sourcegraph/common",
    ":node_modules/@sourcegraph/http-client",
    ":node_modules/@sourcegraph/shared",
    ":node_modules/@sourcegraph/web",
    ":node_modules/@sourcegraph/wildcard",
    ":node_modules/@storybook/svelte",
    ":node_modules/@sveltejs/adapter-static",
    ":node_modules/@sveltejs/kit",
    ":node_modules/@sveltejs/vite-plugin-svelte",
    ":node_modules/@types/prismjs",
    ":node_modules/@urql/core",
    ":node_modules/graphql",
    ":node_modules/prismjs",
    ":node_modules/sass",
    ":node_modules/svelte",
    ":node_modules/ts-key-enum",
    ":node_modules/vite",
    ":node_modules/vite-plugin-inspect",
    ":node_modules/wonka",
    "//:node_modules/@apollo/client",
    "//:node_modules/@codemirror/autocomplete",
    "//:node_modules/@codemirror/commands",
    "//:node_modules/@codemirror/state",
    "//:node_modules/@codemirror/view",
    "//:node_modules/@mdi/js",
    "//:node_modules/@reach/combobox",
    "//:node_modules/@reach/menu-button",
    "//:node_modules/@types/lodash",
    "//:node_modules/@types/node",
    "//:node_modules/classnames",
    "//:node_modules/date-fns",
    "//:node_modules/highlight.js",
    "//:node_modules/lodash-es",
    "//:node_modules/open-color",
    "//:node_modules/path-browserify",
    "//:node_modules/react-resizable",
    "//:node_modules/rxjs",
    "//:node_modules/uuid",
    "//cmd/frontend/graphqlbackend:graphql_schema",
]

CONFIGS = [
    "//client/branded:tsconfig",
    "//client/common:tsconfig",
    "//client/http-client:tsconfig",
    "//client/shared:tsconfig",
    "//client/web:tsconfig",
    "//client/wildcard:tsconfig",
    "//:tsconfig",
    "//:postcss_config_js",
]

npm_link_all_packages(name = "node_modules")

vite_bin.vite(
    name = "web-sveltekit",
    srcs = SRCS + BUILD_DEPS + CONFIGS,
    args = [
        "build",
        "-c vite.config.ts",
    ],
    chdir = package_name(),
    env = {
        "BAZEL": "1",
    },
    out_dirs = ["build"],
    visibility = ["//client/web/dist:__pkg__"],
    # silent_on_success = False,
)

# TODO: remove this once we have some tests.
build_test(
    name = "vite_build_test",
    targets = [":web-sveltekit"],
)

TESTS = glob([
    "src/**/*.test.ts",
    "src/**/*.snap",
])

TEST_BUILD_DEPS = [
    ":node_modules/vitest",
    ":node_modules/signale",
    ":node_modules/@testing-library/svelte",
    ":node_modules/@testing-library/user-event",
]

vitest_test(
    name = "unit_tests",
    bin = vitest_bin,
    chdir = package_name(),
    data = SRCS + BUILD_DEPS + CONFIGS + TESTS + TEST_BUILD_DEPS,
    with_vitest_config = False,
)
